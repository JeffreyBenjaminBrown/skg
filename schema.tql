define
  # Each id identifies exactly one node.
  # Most nodes only have one id, but see `extra_id` below.
  attribute id, value string;

  # Like the relationship
  # from a document to a section thereof, or
  # from a section to a subsection, or
  # from an org headline to its subheadlines.
  # See `docs/vocabulary.md` or `docs/data-model-trees-with-links.md`.
  relation contains,
    relates container,
    relates contained;

  # these are not stored to disk,
  # but rather inferred by rust from a node's text content
  # and from there loaded into typedb.
  # textlinks are org-roam style plain text,
  # which look like
  #  [[id:id] [label:string]]
  relation textlinks_to,
    relates source,
    relates dest;

  # to understand the relations
  #   subscribes,
  #   hides_from_its_subscriptions, and
  #   overrides_view_of,
  # see `docs/sharing-model.md`.
  relation subscribes,
    relates subscriber,
    relates subscribee;
  relation hides_from_its_subscriptions,
    relates hider,
    relates hidden;
  relation overrides_view_of,
    relates replacement,
    relates replaced;

  # "the graph" is made of nodes, which resemble org-headlines.
  entity node,
    owns id @key,
    plays contains:container,
    plays contains:contained,
    plays textlinks_to:source,
    plays textlinks_to:dest,
    plays subscribes:subscriber,
    plays subscribes:subscribee,
    plays hides_from_its_subscriptions:hider,
    plays hides_from_its_subscriptions:hidden,
    plays overrides_view_of:replacement,
    plays overrides_view_of:replaced,
    plays has_extra_id:node;

  entity extra_id,
    # a typical node only has one id, its primary key. but when nodes are merged, `extra_id` is used to associate more than one id with the merge result. the naming is a little awkward, because an `extra_id` is not itself an `id`; rather, it has an `id` field. (if attributes could be members of relationships, the `extra_id` entity would not be needed.)
    owns id @key,
    plays has_extra_id:extra_id;

  relation has_extra_id,
    relates node,
    relates extra_id;
